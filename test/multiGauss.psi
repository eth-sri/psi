// skipped

n := 2; // global bound on the dimensions of all vectors and matrices

def multiGaussCholesky(μ: ℝ[], D: ℝ[][]){ // multivariate Gaussian based on cholesky decomposition of Σ.
	r := array(μ.length,0:ℝ);
	for i in [0..n){
		if i < r.length{
			r[i] = gauss(0,1);
		}
	}
	return avv(mmv(D,r),μ);
}

def multiGauss(μ: ℝ[], Σ: ℝ[][]){
	return multiGaussCholesky(μ,cholesky(Σ));
}

def main(){ // demo for multivariate Gaussian prior
	μ := [0,0];
	Σ := [[1,3/5],[3/5,1]];
	//Σ := [[1,2],[2,4]];
	//μ := [0,0,0];
	//Σ :=[[1,2,1],[2,4,2],[1,2,2]];
	r := multiGauss(μ,Σ); // using cholesky decomposition (somewhat slow)
	//r := sampleFrom("(x,y)=>5·e^(-25·x²·⅟41+-25·y²·⅟82+15·x·y·⅟41)·⅟2·⅟π·⅟√4̅1̅"); // specifying PDF directly (very fast)
	return (r[0],r[1]); // expected: 5/8·e^(-25/32·r₀²+-25/32·r₁²+15/16·r₀·r₁)·λ[r₀]·λ[r₁]·⅟π
	//return (r[0],r[1],r[2]);
	//D := cholesky(Σ);
	/+return (Expectation(r[0]*r[0]),Expectation(r[0]*r[1]),Expectation(r[0]*r[2]),
Expectation(r[1]*r[1]),Expectation(r[1]*r[2]),Expectation(r[2]*r[2]));+/
		//return ((D[0][0],D[1][0],D[1][1],D[2][0],D[2][1],D[2][2]),);
}


def mmv(A: ℝ[][], b: ℝ[])pure{ // matrix-vector product
	return sampleFrom("(r;A,b)=>δ([i↦ ∑_j[0≤j]·[j<b.length] A@[i]@[j]·b@[j]] (A.length))[r]",A,b):ℝ[]; // performance hack
}

def avv(a: ℝ[], b: ℝ[])pure{ // vector addition
	return sampleFrom("(r;a,b)=>δ([i↦ a@[i]+b@[i]] (a.length))[r]",a,b):ℝ[]; // performance hack
}

def dot(a: ℝ[], b: ℝ[])pure{ // dot product for first m entries of vector
	return sampleFrom("(r;a,b)=>δ(∑_i[0≤i]·[i<a.length] a@[i]·b@[i])[r]",a,b); // performance hack
}

def cholesky(A: ℝ[][])pure{ // cholesky decomposition
	assert(sampleFrom("(r;A)=>δ([∑_i[0≤i]·[i<A.length]∑_j[0≤j]·[j<A.length]·[A@[i]@[j]≠A@[j]@[i]]=0])[r]",A):𝔹);
	L := array(A.length,array(A.length,0:ℝ));
	for i in [0..n){
		if i < A.length {
			for j in [0..i+1){
				if j < A.length{
					t := dot(L[i][0..j],L[j][0..j]);
					if i == j {
						assert(A[i][j]>=t);
						L[i][j] = (A[i][j]-t)^(1/2)
					}else if L[j][j]!=0{
						L[i][j] = (A[i][j]-t)/L[j][j];
					}
				}
			}
		}
	}
	return L;
}
